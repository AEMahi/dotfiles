#!/usr/bin/env zsh

local table_width=36
local colorscheme=(

  0   black         '#cccccc'  '\e[0;30m' 
  1   red           '#ffc7c7'  '\e[0;31m' 
  2   green         '#ceffc9'  '\e[0;32m' 
  3   yellow        '#fdf7cd'  '\e[0;33m' 
  4   blue          '#c4effa'  '\e[0;34m' 
  5   magenta       '#eccef0'  '\e[0;35m' 
  6   cyan          '#8ae7c5'  '\e[0;36m' 
  7   white         '#f4f3f2'  '\e[0;37m'

  8   brightblack   '#5c617d'  '\e[0;90m'
  9   brightred     '#f096b7'  '\e[0;91m'
  10  brightgreen   '#d2fd9d'  '\e[0;92m'
  11  brightyellow  '#f3b175'  '\e[0;93m'
  12  brightblue    '#80d7fe'  '\e[0;94m'
  13  brightmagenta '#c9ccfb'  '\e[0;95m'
  14  brightcyan    '#47e7b1'  '\e[0;96m'
  15  brightwhite   '#ffffff'  '\e[0;97m'

  248 grey          '#313244'  '\e[38;5;248m'
  ''  reset         ''         '\e[0m'
)
for ((i=1; i<${#colorscheme[@]}; i+=4)); do local "${colorscheme[i+1]}"="${colorscheme[i+3]}"; done

# Create Color Table Header
print_separator() { printf "${brightblack}%-${table_width}s${reset}\n" | tr ' ' '-' }

print_separator
printf "${white}%-4s %-14s %-8s %-8s${reset}\n" 'VGA' 'COLOR' 'HEX' 'ANSI'
print_separator

# Create Color Table Rows
for ((i=1; i<${#colorscheme[@]}; i+=4)); do
  local vga="${colorscheme[i]}"
  local name="${colorscheme[i+1]}"
  local hex="${colorscheme[i+2]}"
  local ansi="${colorscheme[i+3]}"

  if [[ "$name" == 'reset' ]]; then
    local linecolor=''
  else
    local linecolor=${(P)${:-$name}}
  fi

  # Indirect Substitution of name as color variable
  printf "${linecolor}%-4s %-14s %-8s %-8s${reset}\n" "$vga" "$name" "$hex" "$ansi"
done
